# Copyright (c) 2021-2022 Huawei Device Co., Ltd.
# Licensed under the Apache License, Version 2.0 (the "License");
# you may not use this file except in compliance with the License.
# You may obtain a copy of the License at
#
#     http://www.apache.org/licenses/LICENSE-2.0
#
# Unless required by applicable law or agreed to in writing, software
# distributed under the License is distributed on an "AS IS" BASIS,
# WITHOUT WARRANTIES OR CONDITIONS OF ANY KIND, either express or implied.
# See the License for the specific language governing permissions and
# limitations under the License.

import("//build/ohos.gni")
import("../windowmanager_aafwk.gni")

config("libwms_config") {
  visibility = [ ":*" ]

  include_dirs = [
    "include",
    "include/utils",
    "include/window_snapshot",
    "include/window_group",
    "//utils/system/safwk/native/include",
    "../interfaces/innerkits/wm",
    "../interfaces/innerkits/dm",
    "../wm/include",
    "../utils/include",
    "../dm/include",
    "../dmserver/include",
  ]
}

config("libmockwms_config") {
  visibility = [ ":*" ]

  include_dirs = [
    "include",
    "${window_base_path}/utils/include",
    "${window_base_path}/interfaces/innerkits/wm",
    "${window_base_path}/window_scene/session_manager_service/include",
    "${window_base_path}/window_scene/session_manager/include/zidl",

    # for session_manager
    "${window_base_path}/interfaces/innerkits/wm",
    "${window_base_path}/interfaces/include",
    "${window_base_path}/window_scene",
    "${window_base_path}/window_scene/interfaces/include",
    "${window_base_path}/window_scene/session_manager/include",
    "${window_base_path}/window_scene/session_manager/include/zidl",
    "${window_base_path}/window_scene/session_manager_service/include",

    # for session_manager
    "${multimodalinput_path}/input/interfaces/native/innerkits/event/include",
    "${multimodalinput_path}/input/util/common/include",
    "${ability_runtime_inner_api_path}/ability_manager/include",
    "${window_base_path}/wmserver/include",
    "${window_base_path}/wmserver/include/zidl",

    # for screen_session_manager
    "${window_base_path}/utils/include",
    "${window_base_path}/dm/include",
    "${window_base_path}/interfaces/innerkits/dm",
    "${window_base_path}/dmserver/include",

    # for scene session manager
    "${window_base_path}/interfaces/innerkits/wm",
    "${window_base_path}/wmserver/include",
    "${graphic_base_path}/graphic_2d/rosen/modules/animation/window_animation/include",
    "${graphic_base_path}/graphic_2d/rosen/modules/platform/image_native",

    # for window_manager_hilog
    "${window_base_path}/utils/include",

    # for screenlock_manager_interface
    "${screenlock_mgr_path}/services/include",
    "${screenlock_mgr_path}/utils/include",
    "${screenlock_mgr_path}/frameworks/native/include",
    "${window_base_path}/wm/include",
    "${window_base_path}/wm/include/zidl",
  ]
}

ohos_shared_library("sms") {
  sources = [
    "${window_base_path}/window_scene/session_manager/src/zidl/scene_session_manager_proxy.cpp",
    "src/mock_session_manager_service.cpp",
    "src/zidl/mock_session_manager_service_stub.cpp",
  ]

  public_configs = [ ":libmockwms_config" ]

  deps = [
    "${window_base_path}/utils:libwmutil",
    "${window_base_path}/window_scene/session_manager_service:session_manager_service",
  ]
  external_deps = [
    "c_utils:utils",
    "graphic_2d:librender_service_client",
    "hilog:libhilog",
    "ipc:ipc_single",
    "multimedia_image_framework:image_native",
    "safwk:system_ability_fwk",
  ]
  innerapi_tags = [ "platformsdk" ]
  part_name = "window_manager"
  subsystem_name = "window"
}

ohos_shared_library("libwms") {
  defines = []
  if (use_musl) {
    if (use_jemalloc && use_jemalloc_dfx_intf) {
      defines += [ "CONFIG_USE_JEMALLOC_DFX_INTF" ]
    }
  }
  sources = [
    "../wm/src/zidl/window_manager_agent_proxy.cpp",
    "../wm/src/zidl/window_proxy.cpp",
    "src/accessibility_connection.cpp",
    "src/avoid_area_controller.cpp",
    "src/display_group_controller.cpp",
    "src/display_group_info.cpp",
    "src/display_zoom_controller.cpp",
    "src/drag_controller.cpp",
    "src/freeze_controller.cpp",
    "src/inner_window.cpp",
    "src/input_window_monitor.cpp",
    "src/memory_guard.cpp",
    "src/minimize_app.cpp",
    "src/remote_animation.cpp",
    "src/starting_window.cpp",
    "src/utils/window_manager_service_utils.cpp",
    "src/window_common_event.cpp",
    "src/window_controller.cpp",
    "src/window_dumper.cpp",
    "src/window_group_mgr.cpp",
    "src/window_inner_manager.cpp",
    "src/window_layout_policy.cpp",
    "src/window_layout_policy_cascade.cpp",
    "src/window_layout_policy_tile.cpp",
    "src/window_manager_agent_controller.cpp",
    "src/window_manager_config.cpp",
    "src/window_manager_service.cpp",
    "src/window_node.cpp",
    "src/window_node_container.cpp",
    "src/window_node_state_machine.cpp",
    "src/window_pair.cpp",
    "src/window_root.cpp",
    "src/window_snapshot/snapshot_controller.cpp",
    "src/window_snapshot/snapshot_proxy.cpp",
    "src/window_snapshot/snapshot_stub.cpp",
    "src/window_system_effect.cpp",
    "src/window_zorder_policy.cpp",
    "src/zidl/window_manager_stub.cpp",
  ]

  configs = [
    ":libwms_config",
    "../resources/config/build:coverage_flags",
  ]

  deps = [
    "${window_base_path}/window_scene/interfaces/innerkits:libwsutils",
    "../dmserver:libdms",
    "../etc:wms_etc",
    "../utils:libwmutil",
    "../wm:libwm",
    "//third_party/libxml2:libxml2",
  ]

  external_deps = [
    "ability_base:want",
    "ability_runtime:ability_manager",
    "c_utils:utils",
    "common_event_service:cesfwk_innerkits",
    "config_policy:configpolicy_util",
    "display_manager:displaymgr",
    "eventhandler:libeventhandler",
    "graphic_2d:librender_service_client",
    "graphic_2d:window_animation",
    "hicollie:libhicollie",
    "hilog:libhilog",
    "hisysevent:libhisysevent",
    "hitrace:hitrace_meter",
    "input:libmmi-client",
    "ipc:ipc_single",
    "multimedia_image_framework:image_native",
    "power_manager:powermgr_client",
    "safwk:system_ability_fwk",
  ]

  if (defined(global_parts_info) &&
      defined(global_parts_info.resourceschedule_memmgr_plugin)) {
    external_deps += [ "memmgr_plugin:memmgrclient" ]
    defines += [ "MEMMGR_WINDOW_ENABLE" ]
  }

  if (defined(global_parts_info) &&
      defined(global_parts_info.resourceschedule_resource_schedule_service)) {
    external_deps += [ "resource_schedule_service:ressched_client" ]
    defines += [ "RESOURCE_SCHEDULE_SERVICE_ENABLE" ]
  }

  if (defined(global_parts_info) &&
      defined(global_parts_info.resourceschedule_soc_perf)) {
    external_deps += [ "soc_perf:socperf_client" ]
    defines += [ "SOC_PERF_ENABLE" ]
  }

  if (is_standard_system) {
    external_deps += [ "init:libbegetutil" ]
  } else {
    external_deps += [ "init_lite:libbegetutil" ]
  }

  part_name = "window_manager"
  subsystem_name = "window"
}

group("test") {
  testonly = true
  deps = [ "test:test" ]
}
